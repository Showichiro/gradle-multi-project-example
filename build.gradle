plugins {
    id 'java-library'
	id 'org.springframework.boot' version '3.1.0-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.0'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management' 

    group = 'gradle.multi.project.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

}

tasks.named('test') {
    useJUnitPlatform()
}

project(":web") {
    dependencies {
	    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.postgresql:postgresql:42.6.0'
        api project(':service')
    }
}


project(':repository') {
    bootJar {
        enabled = false
    }
    dependencies {
	    api 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }
}

project(':service') {
    bootJar {
        enabled = false
    }
    dependencies {
        api project(':repository')
    }
}

project(':api') {
    dependencies {
	    implementation 'org.springframework.boot:spring-boot-starter-graphql'
        implementation 'org.postgresql:postgresql:42.6.0'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        api project(':service')
    }
}